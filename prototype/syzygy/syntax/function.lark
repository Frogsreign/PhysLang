// Imports
%import .identifier.identifier -> identifier
%import .identifier.particle_property_access -> particle_property_access
%import .identifier.particle_name -> particle_name
%import .identifier.property_name -> property_name
%import .identifier.property_index -> property_index
%import .identifier.keyword -> keyword

%import .literal.literal -> literal
%import .literal.literal_scalar -> literal_scalar
%import .literal.literal_vector -> literal_vector

%import common.WS
%ignore WS

// Order of operations
start: expr

?expr: term // almost always inline
    | add
    | sub

?term: factor // `term` to match the Jonas-syntax
    | mul
    | div

?factor: literal
    | identifier
    | builtin
    | pow
    | "(" expr ")"


?builtin: dot
  | norm
  | abs
  | step


// Binary operations
add: expr "+" term

sub: expr "-" term

mul: term "*" factor

div: term "/" factor

pow: term "^" factor

dot: "dot(" expr "," expr ")"
    | "<" expr "," expr ">" // inner product notation


// Unary operators
norm: "norm(" expr ")"

abs: "abs(" expr ")"

step: "step(" expr ")"
